Dim swApp As SldWorks.SldWorks
Dim swModel As SldWorks.ModelDoc2
Dim swDraw As SldWorks.DrawingDoc

 

Dim vConfs As Variant
Dim i As Integer

 

Dim sDrTemplate As String
Dim lDrSize As Long

 

Const sOutputFolder As String = "C:\Users\james.veale\Desktop\Mesh Drawings\"

 

Sub main()
   
    Set swApp = Application.SldWorks
   
    Set swModel = swApp.ActiveDoc
       
    sDrTemplate = swApp.GetUserPreferenceStringValue(swUserPreferenceStringValue_e.swDefaultTemplateDrawing)
    lDrSize = swDwgPaperSizes_e.swDwgPaperA0size
   
    vConfs = swModel.GetConfigurationNames()
   
    For i = 0 To UBound(vConfs)
       
        Set swDraw = swApp.NewDocument(sDrTemplate, lDrSize, 0, 0)
        swDraw.Create1stAngleViews2 swModel.GetPathName
        Dim swView As SldWorks.View
        Set swView = swDraw.GetFirstView
       
        While Not swView Is Nothing
           
            swView.ReferencedConfiguration = vConfs(i)
           
            Set swView = swView.GetNextView
           
        Wend
       
        Dim swDrawModel As SldWorks.ModelDoc2
        Set swDrawModel = swDraw
              
        swDrawModel.ForceRebuild3 False
       
        swDraw.InsertModelAnnotations3 swImportModelItemsSource_e.swImportModelItemsFromEntireModel, 32776, True, True, False, False
               
        swDrawModel.Extension.SaveAs sOutputFolder + swModel.GetTitle() + "_" + vConfs(i) + ".slddrw", swSaveAsVersion_e.swSaveAsCurrentVersion, swSaveAsOptions_e.swSaveAsOptions_Silent, Nothing, 0, 0
       
        swApp.CloseDoc swDrawModel.GetTitle()
       
    Next
   
End Sub